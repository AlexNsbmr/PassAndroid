buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath('com.stanfy.spoon:spoon-gradle-plugin:1.0.1') {
            exclude module: "guava"
        }
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    jcenter()
    mavenLocal()
}

android {

    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        versionCode 255
        versionName "2.5.5"
        minSdkVersion 9
        targetSdkVersion 22
        applicationId "org.ligi.passandroid"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "maps", "analytics", "distribution"

    productFlavors {

        withMaps {
            dimension "maps"
        }

        noMaps {
            dimension "maps"
        }

        withAnalytics {
            dimension "analytics"
        }

        noAnalytics {
            dimension "analytics"
        }

        forFDroid {
            dimension "distribution"
        }

        forPlay {
            dimension "distribution"
        }
    }

    // needed for assertJ
    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
    }
}


configurations {
    // http://qathread.blogspot.de/2015/02/discovering-espresso-for-android.html
    androidTestCompile.exclude group: 'com.android.support', module: 'support-v4'
}

dependencies {
    apt 'org.projectlombok:lombok:1.16.4'

    androidTestCompile 'com.android.support:support-v4:22.2.0'
    androidTestCompile 'com.android.support:support-annotations:22.2.0'

    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'

    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.9'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'

    // have to wait with update till this is solved: https://code.google.com/p/dexmaker/issues/detail?id=40
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    // The Apache Software License, Version 2.0
    compile 'com.googlecode.android-query:android-query:0.25.9'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'net.lingala.zip4j:zip4j:1.3.2'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'net.danlew:android.joda:2.8.0'
    compile 'com.squareup:otto:1.3.7'

    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'

    compile 'net.i2p.android.ext:floatingactionbutton:1.9.0'

    compile 'org.ligi:AXT:0.33'
    compile 'org.ligi:tracedroid:1.4'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'fr.nicolaspomepuy:discreetapprate:2.0.4@aar'
    compile 'com.larswerkman:HoloColorPicker:1.4'
    compile 'com.google.code.findbugs:jsr305:3.0.0'

    compile 'net.steamcrafted:load-toast:1.0.6'

    provided "org.projectlombok:lombok:1.16.4"

    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.0'

    compile('com.mcxiaoke.viewpagerindicator:library:2.4.1') {
        exclude group: 'com.android.support'
    }
    // cannot use this new version: https://github.com/zxing/zxing/issues/165
    // WARNING: might work on some devices or the emulator - but fails on others
    // compile 'com.google.zxing:core:3.1.0'

    compile fileTree(dir: 'libs', include: 'zxing-core-2.3.0-SNAPSHOT.jar')

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    withAnalyticsCompile 'com.google.android.gms:play-services-analytics:7.5.0'
    withMapsCompile 'com.google.android.gms:play-services-maps:7.5.0'
}
